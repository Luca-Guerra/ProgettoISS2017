System a0

Event sonar : p(Distance, Angle)

Dispatch sonarreached : sonarreached
Dispatch robotstart   : robotstart
Dispatch robotstop    : robotstop

// ------------------------------------------------------------------
//			CONTEXTS
// ------------------------------------------------------------------
Context ctxBase ip [ host="localhost" port=8079] -g green

EventHandler evh_sonar for sonar -print{ 
	memo currentEvent for console
};

QActor robot context ctxBase{
	Plan init normal
		println("ROBOT: Ready" );
		switchToPlan waitConsoleStart
	
	Plan waitConsoleStart
		println("ROBOT: I'm waiting start msg" );
		/*
		 * Wait for 5 minute the start, after this lapse of time the Robot shutdown
		 */
		receiveMsg time(300000);
		onMsg robotstart:robotstart -> switchToPlan goStraight else endPlan "ROBOT: finish!";
		repeatPlan
		
	/*		
 	 *  SCENARIO: 		
 	 * 	ID          : Robot go straight on		
 	 * 	DESCRIPTION : The Robot continues in a straight line to the B zone		
 	 */
	Plan goStraight resumeLastPlan
		println("ROBOT: goStraight");
		demo assign(state,gostraight);
		receiveMsg time(1000);
		onMsg sonarreached:sonarreached -> switchToPlan sonarReached;
		onMsg robotstop : robotstop -> switchToPlan stopTheRobot;
		repeatPlan
	
	/*
	 *  SCENARIO: 
	 * 	ID          : Robot Reaches Sonar
	 * 	DESCRIPTION : The Robot reaches the signal emitted by the Sonar
	 */
	Plan sonarReached resumeLastPlan
		demo assign(state,sonarreached);
		println("ROBOT: Stop, Turn Left, Start Blinking, Take Pictures, Send Photo, Stop Blinking, Turn Right, Restart");
		delay time(2000) // simulo il tempo di operazione
	
	Plan stopTheRobot
		println("ROBOT: Stop");
		demo assign(state,stop);
		switchToPlan waitConsoleStart
}

QActor console context ctxBase {
	Rules{
		/*
		 * CONFIGURATION
		 */
		value(robotmove, 0). // stato del robot
		value(nsonars, 1).
	}
	Plan init normal
		// connection to MQTT server to show the sensors distance
		actorOp connectToSend("guersant", "tcp://m2m.eclipse.org:1883", "unibo/mqtt/radar");
		actorOp activateGui;
		println("CONSOLE: ready!");
		switchToPlan checkSystemStart
		
	Plan checkSystemStart
		// if Robot is in area A init the System
		[ ?? value(area_a, 1) ] switchToPlan initSystem;
		delay time(1000);
		repeatPlan
	
	/*
	 *  SCENARIO: 
	 * 	ID          : Restart the system
	 * 	DESCRIPTION : The Robot reaches the signal emitted by the Sonar
	 */
	Plan checkRestartSystem resumeLastPlan
		// if the Robot is in area A restart the System
		[ ?? value(area_a, 1) ] switchToPlan initSystem
	
	Plan initSystem resumeLastPlan
		demo assign(sonarreached, 0);
		[ ?? value(dist,V)] println("clean");
		// if robot is stopped restart
		[ !? value(robotmove, 0)] forward robot -m robotstart:robotstart;
		[ !? value(robotmove, 0)] demo assign(robotmove, 1);
		switchToPlan senseSonar
		
	Plan senseSonar resumeLastPlan
		[ !? msg( MSGID, MSGTYPE, SENDER, RECEIVER, p(D,A), SEQNUM )] switchToPlan workflow;
		switchToPlan checkRestartSystem; // control the system restart
		repeatPlan
		
	Plan workflow resumeLastPlan	
		// show distance in Radar
		[ !? msg( MSGID, MSGTYPE, SENDER, RECEIVER, p(D,A), SEQNUM )] actorOp sendMsgMqtt("unibo/mqtt/radar","polar","guersant", p(D, 90));
		// memorize the distance
		[ !? msg( MSGID, MSGTYPE, SENDER, RECEIVER, p(D,A), SEQNUM )] actorOp memoSonarEvent(D, A);
		[ ?? msg( MSGID, MSGTYPE, SENDER, RECEIVER, p(D,A), SEQNUM )] switchToPlan checkSonarReached
	
	/*
	 *  SCENARIO: 
	 * 	ID          : Robot Reaches Sonar
	 * 	DESCRIPTION : The Robot reaches the signal emitted by the Sonar
	 */
	Plan checkSonarReached resumeLastPlan
		actorOp sonarReached;
		[ !? actorOpDone(sonarReached,1)] println("CONSOLE: sonarReached!");
		// for UnitTest
		[ !? actorOpDone(sonarReached,1)] demo assign(state,sonarreached);
		// memorize the last sonar reached
		[ !? actorOpDone(sonarReached,1)] demo inc(sonarreached,1,X);
		[ !? actorOpDone(sonarReached,1)] forward robot -m sonarreached:sonarreached;
		[ ?? actorOpDone(sonarReached,1)] switchToPlan checkAreaB
		
		
	/*
	 *  SCENARIO: 
	 * 	ID          : RobotReachBZone
	 * 	DESCRIPTION : Robot reaches B zone and stops
	 */
	Plan checkAreaB resumeLastPlan
		actorOp inAreaB;
		[ ?? actorOpDone(inAreaB, 1)] switchToPlan stopConsole
		
	Plan stopConsole
		forward robot -m robotstop : robotstop;
		// update the Robot's state in its KB
		demo assign(robotmove, 0);
		println("CONSOLE: stop!");
		switchToPlan checkSystemStart
}

QActor sensorsonar context ctxBase  {    
	Rules{
		/* SIMULATION DATA */
		//simulation.	
		p(58,59).p(54,68).p(51,79).p(50,90).p(51,101).
		p(54,112).p(58,121).p(64,129).p(71,135).p(78,140).
		p(86,144).p(94,148).p(103,151).p(112,153).p(121,156).
		p(130,157).p(139,159).p(149,160).p(158,162).
	}	  
 	Plan init normal     
 		println("SONARS: Start");
 		[ !? simulation ] switchToPlan workSimulate else switchToPlan work;
  		println("SONARS: finish")      
 	Plan workSimulate   resumeLastPlan
  	 	[ !? p(D, A) ] println(p(D, A)); 
		[ ?? p(D, A) ] emit sonar : p(D, A);
		delay time(5000);
		repeatPlan 15
	Plan work resumeLastPlan
		repeatPlan
}  